# Homebrew update, upgrade, cleanup, doctor
function bs() {
  local LOGFILE="/tmp/brewlog-$(/bin/date -j '+%Y%m%d')"
  local YELLOW="\033[1;33m"
  local NC="\033[0m"

  echo "${YELLOW}LOGFILE:${NC} $LOGFILE"
  for command in update upgrade cleanup doctor; do
    echo "\n${YELLOW}COMMAND:${NC} ${command}\n"
    brew "$command" 2>&1 \
      | tee -a "$LOGFILE"
  done
}

# Open Drupal project webpage
function dp() {
  open "https://www.drupal.org/project/${1}"
}

# Fast Vim:Shell Switching
function fancy-ctrl-z() {
  if [[ $#BUFFER -eq 0 ]]; then
    BUFFER="fg"
    zle accept-line
  else
    zle push-input
    zle clear-screen
  fi
}
zle -N fancy-ctrl-z
bindkey '^Z' fancy-ctrl-z

# SSH Public Key Fingerprints
# http://serverfault.com/questions/413231/how-to-get-all-fingerprints-for-ssh-authorized-keys2-file
function fingerprints() {
  local hash=${2:-'md5'}
  local file="$1"
  while read l; do
    [[ -n $l && ${l###} = $l ]] && ssh-keygen -E "${hash}" -l -f /dev/stdin <<<$l
  done < $file
}

# HTTP Status info
function hs() {
  curl -sk "http://httpstat.us/${1}"
  printf "\n"
}

# Create directory and cd
function mcd() {
  mkdir -p "$1" && cd "$1"
}

function rand() {
  local LENGTH=$1
  date +%s \
    | shasum \
    | base64 \
    | head -c $LENGTH \
    | tr '[:upper:]' '[:lower:]'
  echo
}

# Open file at line number using colon syntax
# Example: vi file.txt:5
function vi() {
  if [[ $@ =~ ^([^:]+):([0-9]+)$ ]]; then
    nvim "${match[1]}" "+${match[2]}"
  else
    nvim "$@"
  fi
}
